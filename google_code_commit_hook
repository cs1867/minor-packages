#!/usr/bin/perl

use strict;
use warnings;

use CGI;
use JSON;
use Data::Dumper;
use Mail::Sendmail;
use Template;

use Email::MIME::Creator;
use Email::Send;

use Git;

my $mail_to        = 'ps-ps-dev@internet2.edu';
my $mail_from      = 'svnlog@internet2.edu';
my $temp_directory = '/tmp/git_post_hook';

my $subject_template = << 'EOF';
[[% project_name %][%- IF sub_repository_name %].[% sub_repository_name %][% END %]] [% revision_count %] revision(s) committed
EOF

my $mail_template  =  <<'EOF';
[% USE date %]

[% revision_count %] new revision(s)

[%- FOREACH rev IN revisions -%]
[% IF rev.full_diff or rev.summary_diff %]==============================================================================[% END %]

Author: [% rev.author %]
Date: [% date.format(rev.timestamp) %]
Log: [% rev.message %]
[% project_path %]/source/detail?r=[% rev.revision %][% IF sub_repository_name %]&repo=[% sub_repository_name %][% END %]
[% IF rev.full_diff or rev.summary_diff %]
Changes:
[% rev.summary_diff %]
[% IF rev.full_diff.length < 4096 %] [% rev.full_diff %] [% END %]
[% END %]
[% END %]
EOF

my $query = CGI->new();
my $data = $query->param('POSTDATA');

unless ($data) {
    die("No data in request");
}

my $json = JSON->new()->decode($data);

print $query->header;

my $testing = $json->{testing};

my $repo_url = $json->{repository_path};

my $repo_path = $repo_url;

$repo_path =~ s/[^a-zA-Z0-9.]/_/g;

$repo_path = $temp_directory . "/" . $repo_path;

if (! -d $repo_path ) {
    print "mkdir -p $repo_path\n";

    `mkdir -p $repo_path`;
    if ($? != 0) {
        die("Couldn't create $repo_path");
    }

    `git clone $repo_url $repo_path`;
    if ($? != 0) {
        die("Couldn't create $repo_path");
    }

    eval {
        Git::command_oneline(['clone', $repo_url, $repo_path], STDERR => 0);
    };
    if ($@) {
        die("Couldn't checkout $repo_url: ".$@);
    }
}

my $repo = Git->repository(Directory => $repo_path);

eval {
    $repo->command_oneline(['pull'], STDERR => 0);
};
if ($@) {
    die("Couldn't pull $repo_url: ".$@);
}

foreach my $revision_info (@{ $json->{revisions} }) {
    my $full_diff;
    eval {
        $full_diff = "";

        my ($fh, $c) = $repo->command_output_pipe('diff', $revision_info->{revision}.'^!');
        while(<$fh>) {
            $full_diff .= $_;
        }
        $repo->command_close_pipe($fh, $c);        
    };
    if ($full_diff) {
        $revision_info->{full_diff} = $full_diff;
    }

    my $summary_diff;
    eval {
        $summary_diff = "";

        my ($fh, $c) = $repo->command_output_pipe('diff', '--stat', $revision_info->{revision}.'^!');
        while(<$fh>) {
            $summary_diff .= $_;
        }
        $repo->command_close_pipe($fh, $c);        
    };
    if ($summary_diff) {
        $revision_info->{summary_diff} = $summary_diff;
    }
}

my $project_name = $json->{project_name};
my $repository_path = $json->{repository_path};

if ($repository_path) {
    if ($repository_path =~ /(.*$project_name)\.([^\/]*)/) {
        $json->{project_path} = $1;
        $json->{sub_repository_name} = $2;
    }
    else {
        $json->{project_path} = $repository_path;
    }
}


my $tt = Template->new(TRIM => 1);

my $message = "";
my $subject = "";

unless ($tt->process(\$mail_template, $json, \$message)) {
    my $msg = "Couldn't process email template: ".$tt->error();
    print $msg."\n" if $testing;
    die($msg);
}

unless ($tt->process(\$subject_template, $json, \$subject)) {
    my $msg = "Couldn't process subject template: ".$tt->error();
    print $msg."\n" if $testing;
    die($msg);
}

my $email;
eval {
  $email = Email::MIME->create(
  header => [
    From    => $mail_from,
    To      => $mail_to,
    Subject => $subject,
  ],
  attributes => {
    encoding => 'quoted-printable',
    charset  => 'ISO-8859-1',
  },
  body => $message,
);
};
if ($@) {
    my $msg = "Couldn't make email: $@";
    print $msg."\n" if $testing;
    die($msg);
}

print $subject if $testing;
print $message if $testing;

unless (Email::Send->new({mailer => 'Sendmail'})->send($email->as_string)) {
    my $msg = "Couldn't send email";
    print $msg."\n" if $testing;
    die($msg);
}
